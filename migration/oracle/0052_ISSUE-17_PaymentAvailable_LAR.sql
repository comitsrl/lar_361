CREATE OR REPLACE FUNCTION "ADEMPIERE"."PAYMENTAVAILABLE_LAR"
(
  p_C_Payment_ID  IN  NUMBER
)
RETURN NUMBER
/*************************************************************************
 * The contents of this file are subject to the Compiere License.  You may
 * obtain a copy of the License at    http://www.compiere.org/license.html
 * Software is on an  "AS IS" basis,  WITHOUT WARRANTY OF ANY KIND, either
 * express or implied. See the License for details. Code: Compiere ERP+CRM
 * Copyright (C) 1999-2001 Jorg Janke, ComPiere, Inc. All Rights Reserved.
 *************************************************************************
 * Título: Calcula el importe de pago disponible (sin asignar),
 *         considerando a los cargos por retenciones como pago normales.
 *         Esta función solo se utiliza en la ventana de asignaciones
 *         de LAR (Allocation.java)
 * Descripcion:
 *    similar a PaymentAvailable
 ************************************************************************/
AS
  v_Currency_ID   NUMBER(10);
  v_AvailableAmt  NUMBER := 0;
  v_IsReceipt     C_Payment.IsReceipt%TYPE;
  v_Amt           NUMBER := 0;
  CURSOR  Cur_Alloc IS
    SELECT a.AD_Client_ID, a.AD_Org_ID, al.Amount, a.C_Currency_ID, a.DateTrx
      FROM C_AllocationLine al
     INNER JOIN C_AllocationHdr a ON (al.C_AllocationHdr_ID=a.C_AllocationHdr_ID)
     WHERE al.C_Payment_ID = p_C_Payment_ID
       AND a.IsActive='Y';
    --  AND al.C_Invoice_ID IS NOT NULL;
BEGIN
  --  Charge - fully allocated
  SELECT MAX(PayAmt)
    INTO v_Amt
    FROM C_Payment
   WHERE C_Payment_ID=p_C_Payment_ID
     AND C_Charge_ID > 0
     AND EsRetencionIIBB = 'N';
  IF (v_Amt IS NOT NULL) THEN
      RETURN 0;
  END IF;

  --  Get Currency
  SELECT C_Currency_ID, PayAmt, IsReceipt
    INTO v_Currency_ID, v_AvailableAmt, v_IsReceipt
    FROM C_Payment_v     -- corrected for AP/AR
   WHERE C_Payment_ID = p_C_Payment_ID;
--  DBMS_OUTPUT.PUT_LINE('== C_Payment_ID=' || p_C_Payment_ID || ', PayAmt=' || v_AvailableAmt || ', Receipt=' || v_IsReceipt);

  --  Calculate Allocated Amount
  FOR a IN Cur_Alloc LOOP
      v_Amt := currencyConvert(a.Amount, a.C_Currency_ID, v_Currency_ID, a.DateTrx, null, a.AD_Client_ID, a.AD_Org_ID);
      v_AvailableAmt := v_AvailableAmt - v_Amt;
--      DBMS_OUTPUT.PUT_LINE('  Allocation=' || a.Amount || ' - Available=' || v_AvailableAmt);
  END LOOP;
  --  Ignore Rounding
  IF (v_AvailableAmt BETWEEN -0.00999 AND 0.00999) THEN
    v_AvailableAmt := 0;
  END IF;
  --  Round to penny
  v_AvailableAmt := ROUND(NVL(v_AvailableAmt,0), 2);
  RETURN  v_AvailableAmt;
END PaymentAvailable_LAR
;
